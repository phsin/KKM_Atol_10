Перем тпДокументы, тзДокументы;
Перем Атол10;
Перем оПривязки; //:Меркурий.Привязки

//Служебные функции
Функция Этот(Конт) 	Возврат Конт; КонецФункции
Функция Сам() 	Возврат Этот(Контекст); КонецФункции

Процедура ЗаголовокНадпись()

	форма.НачалоПериодаТекст.Заголовок(СокрЛП(НачДата));
	форма.КонецПериодаТекст.Заголовок(сокрлп(КонДата));

КонецПроцедуры // ЗаголовокНадпись

Процедура ЗаполнитьСписокДокументов()
	
	//t1 = ГМ.Старт();

	тзДокументы.УдалитьСтроки();	
	
	Док = СоздатьОбъект("Документ.Реализация");
	Док.ВыбратьДокументы( НачДата, КонДата );
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если Док.Проведен()=0 Тогда
			Продолжить; 
		КонецЕсли;
		Если Док.Договор.ОплатаТолькоНаличными=0 Тогда
			Продолжить; 
		КонецЕсли;
			
		тзДокументы.НоваяСтрока();
		//тзДокументы.Пометка = ;
		тзДокументы.Док = Док.ТекущийДокумент();
		тзДокументы.Контрагент = Док.Контрагент;
		тзДокументы.Сумма = Док.СуммаВзаиморасчетов;
		Попытка тзДокументы.НомерЧека = Док.НомерЧекаККМ; Исключение КонецПопытки;					
	КонецЦикла;

	Док = СоздатьОбъект("Документ.ПКО");
	Док.ВыбратьДокументы( НачДата, КонДата );
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если Док.Проведен()=0 Тогда 
			Продолжить;
		КонецЕсли;

		тзДокументы.НоваяСтрока();
		//тзДокументы.Пометка = ;
		тзДокументы.Док = Док.ТекущийДокумент();
		тзДокументы.Контрагент = Док.Контрагент;
		тзДокументы.Сумма = Док.Сумма;
		тзДокументы.НомерЧека = Док.НомерЧекаККМ;
	КонецЦикла;
	
	Если ПустоеЗначение(тпДокументы ) = 0 Тогда
		тпДокументы.ОбновитьСтроки(); 
	КонецЕсли;
	              
	//ГМ.Финиш(t1, Сам(), "ЗаполнитьСписокДокументов_Реализации", );
	
КонецПроцедуры

 // предопределенная процедура
 Процедура ПриВыбореЗакладки(НомерЗакладки, ЗначениеЗакладки)
	
	Форма.ИспользоватьСлой(ЗначениеЗакладки);
	
	Форма.Закладки.ТекущаяСтрока(НомерЗакладки);
	
КонецПроцедуры

Процедура ПриИзмененииРазмераОкна(ТипСобытия, НовШирина, НовВысота) Экспорт
	оПривязки.ПриИзмененииРазмераОкна(ТипСобытия, НовШирина, НовВысота);
КонецПроцедуры

Процедура ПослеОткрытия()
	оПривязки.ПослеОткрытия();
КонецПроцедуры

Процедура Привязки_Инит()
	
	оПривязки = СоздатьОбъект("Меркурий.Привязки");
	оПривязки.УстановитьФорму(Форма);
	оПривязки.Привязка("тпДокументы", "H", "Форма", "W", "Форма");
	//оПривязки.Привязка("Версия,Инфо,ИнфоВыбор, тРеализация,тКолвоСписания","T","Форма");

КонецПроцедуры


//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Реализации","Реализации");
	Форма.Закладки.ДобавитьЗначение("Основной","Отладка");
	Форма.Закладки.ДобавитьЗначение("Печать","Групповая печать");

	Форма.ИспользоватьСлой("Реализации");
	
	ЗаголовокНадпись();
	
	Если ПустоеЗначение( Форма.Параметр) = 0 Тогда
		Док      = Форма.Параметр.Получить("Контекст");
		//Устройство = Форма.Параметр.Получить("Устройство");
		//КолвоКопий = Форма.Параметр.Получить("КоличествоКопий");
		//СразуНаПринтер = Число(Форма.Параметр.Получить("СразуПечать"));

		Если ТипЗначенияСтр(Док) = "Реализация" Тогда
			
			НомерЧека = Атол10.ОтгрузкаБезОплаты(Док);
			
		ИначеЕсли ТипЗначенияСтр(Док) = "ПКО" Тогда
			
			НомерЧека = Атол10.ОПЛАТА( Док );
		
		КонецЕсли;

		Попытка
			//НомерЧека =  ПолучитьНомерЧека();
			Форма.Параметр.Установить("НомерЧека", НомерЧека);
			//Форма.Параметр.Установить("НомерЧека", 123);
		Исключение
			Сообщить(ОписаниеОшибки());
			Форма.Параметр.Установить("НомерЧека", "");
		КонецПопытки;

		Статусвозврата(0);
		Возврат;
	КонецЕсли;

	Привязки_Инит();
КонецПроцедуры // ПриОткрытии()

Процедура ПриЗакрытии()	

КонецПроцедуры

//======================================================================
Процедура ПослеСозданияФормы()

	тпДокументы = оПривязки.СоздатьТабличноеПоле( Сам(), "тпДокументы", тзДокументы,1,1);
	
	ЗаполнитьСписокДокументов();
	
	тпДокументы.ОбновитьСтроки(); 

КонецПроцедуры


//======================================================================
процедура ВыбратьПериод()
	
	Если ВвестиПериод(НачДата,КонДата)=1 тогда
		
		ЗаголовокНадпись();
		тзДокументы.УдалитьСтроки();
		ЗаполнитьСписокДокументов();
		
	конецесли;
конецпроцедуры

//========================= ТАБЛИЧНОЕ ПОЛЕ ДОКУМЕНТОВ =================
//{
Процедура тпДокументыПриАктивизацииСтроки(ТабличноеПоле)
	оПривязки.ПриАктивизацииСтрокиТП(ТабличноеПоле, тзДокументы);
КонецПроцедуры

Процедура тпДокументыВыбор()
	оПривязки.ПриАктивизацииСтрокиТП( тпДокументы, тзДокументы);
	оПривязки.ПриАктивизацииКолонкиТП( тпДокументы, тзДокументы);
	
	текСтр = тзДокументы.ТекущаяСтрока();
	Если текСтр=0 Тогда Возврат; КонецЕсли;
	текКол = тзДокументы.ТекущаяКолонка();
	
	Эл = тзДокументы.ПолучитьЗначение(текСтр, текКол);	
	ОткрытьФорму(Эл);
КонецПроцедуры

Процедура тпДокументыПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)

	Если ТипРегиона = 3 Тогда
		оПривязки.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Пометка");
		
		Если (ПустоеЗначение(ДанныеСтроки.НомерЧека)=0) Тогда
			
			ОформлениеСтроки.ЦветФона = 14680031; //ГМ.цвЗеленый;
		КонецЕсли;
		//	
		//	Если (ДанныеСтроки.ВСД.Проведен()=0) Тогда 
		//		
		//		ОформлениеСтроки.ЦветФона = ГМ.цвКрасный;
		//		
		//	ИначеЕсли ГМ.СтатусЗакрыт(ДанныеСтроки.ВСД.Статус)=1 Тогда
				
		//	КонецЕсли;
		//	
		//ИначеЕсли ПустоеЗначение(ДанныеСтроки.ХозСубъект.GUID)=1 Тогда 			
		//	
		//	ОформлениеСтроки.ЦветФона = ГМ.цвКрасный;
		//	
		//ИначеЕсли (ПустоеЗначение(ДанныеСтроки.Площадка)=0) Тогда
		//	
		//	Если (ПустоеЗначение(ДанныеСтроки.Площадка.GUID)=0)  Тогда 
		//		//ДанныеСтроки.сЦвет = "";							
		//	КонецЕсли;
		//				
		//Иначе			
		//	ОформлениеСтроки.ЦветФона = ГМ.цвЖелтый;	
	КонецЕсли;
КонецПроцедуры

Процедура тпДокументыПриВыбореФлажка(ТабличноеПоле,Стр, Колонка, ТипРегиона)

	оПривязки.ПриАктивизацииСтрокиТП(ТабличноеПоле, тзДокументы);
	тзДокументы.Пометка = ?(тзДокументы.Пометка = 1,0,1);
	ТабличноеПоле.ОбновитьСтроки();	

КонецПроцедуры


//}

Процедура ОтменитьВсеДокументы()
	
	тзДокументы.ВыбратьСтроки();
	Пока тзДокументы.ПолучитьСтроку() = 1 Цикл
		тзДокументы.Пометка=0;
	КонецЦикла;

	тпДокументы.ОбновитьСтроки();
	
КонецПроцедуры

Процедура ВыделитьВсеДокументы()
	
	тзДокументы.ВыбратьСтроки();
	Пока тзДокументы.ПолучитьСтроку() = 1 Цикл
		тзДокументы.Пометка=1;
	КонецЦикла;

	тпДокументы.ОбновитьСтроки();
	
КонецПроцедуры

Процедура ОформитьЧеки()
	
	тзДокументы.ВыбратьСтроки();
	Пока тзДокументы.ПолучитьСтроку() = 1 Цикл
		Если тзДокументы.Пометка=1 Тогда
			
			Атол10.ОформитьЧек( ВыбКасса, тзДокументы.Док );
			
		КонецЕсли;
	КонецЦикла;
		
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

Процедура ОформитьЧекиКоррекции()
	
	тзДокументы.ВыбратьСтроки();
	Пока тзДокументы.ПолучитьСтроку() = 1 Цикл
		Если тзДокументы.Пометка=1 Тогда
			
			Атол10.ОформитьЧекКоррекции( ВыбКасса, тзДокументы.Док );
			
		КонецЕсли;
	КонецЦикла;
		
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

//******************************************************************************
// ЗаполнитьПринтеры()
//
// Параметры:
//
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура ЗаполнитьПринтеры()
	Перем Стр;
	//очистим список
	Принтер.УдалитьВсе();

	//заполним список доступных принтеров
	Состояние("Заполняем список принтеров...");
    wshNetwork    = createObject("WScript.Network");
	oPrinters        = wshNetwork.EnumPrinterConnections();
	i = 0;
	Пока i < oPrinters.Count() - 1 Цикл
		Если Найти(oPrinters.Item(i+1),"\\tsclient") > 0 Тогда
			i = i + 2;
			Продолжить;
		КонецЕсли;
		Принтер.ДобавитьЗначение(oPrinters.Item(i+1),oPrinters.Item(i+1));
		i = i + 2;
	КонецЦикла;

    //    Принтер по умолчанию
    wshPrint = CreateObject("WScript.Shell");
    Prn = СокрЛП(wshPrint.RegRead("HKCU\Software\Microsoft\Windows NT\CurrentVersion\Windows\Device")); //принтер по умолчанию, но вместе с портом
	Сч =  1;
	Пока Сч <= Принтер.РазмерСписка() Цикл
		Принтер.ПолучитьЗначение(Сч,Стр);
		Если Найти(Prn,Стр) > 0 Тогда
			//Это и есть принтер по умолчанию
			Прервать;
		КонецЕсли;
		Сч = Сч + 1;
		Стр = "";
	КонецЦикла;
	Принтер.ТекущаяСтрока(Сч);
	Форма.Принтер.Доступность(1);

КонецПроцедуры // ЗаполнитьПринтеры()

//******************************************************************************
Процедура ПриИзмененииСразуНаПринтер()
	Если СразуНаПринтер=1 Тогда
		ЗаполнитьПринтеры();
	Иначе
		Принтер.УдалитьВсе();
		Форма.Принтер.Доступность(0);
	КонецЕсли;
КонецПроцедуры

//{ отчеты

//******************************************************************************
// ПоКнопкеПечать()
//
// Параметры:
//		ВыборФормы =
//   	1, если перед печатью необходимо выбрать печатную форму,
//		0, если надо напечатать, используя последнюю выбранную
//
// Описание:
// 	Вызывается по кнопке "Печать" и по кнопке "Выбор печатной формы" (небольшая
// 	кнопка рядом с кнопкой печати)
//
Процедура ПечатьПКО()

	Док = СоздатьОбъект("Документ.ПКО");
	Док.ВыбратьДокументы(НачДата,КонДата);

	Пока Док.ПолучитьДокумент()=1 Цикл
		Если Док.ПометкаУдаления()=1 Тогда Продолжить;  КонецЕсли;

		Попытка
			Если Выбклиент.Выбран()=1 Тогда
				Если НЕ (Док.Контрагент = Выбклиент) Тогда Продолжить; КонецЕсли;
			КонецЕсли;
		Исключение КонецПопытки;
		Попытка
			Если ВыбАвтор.Выбран()=1 Тогда
				Если НЕ (Док.Автор = ВыбАвтор) Тогда Продолжить; КонецЕсли;
			КонецЕсли;
		Исключение КонецПопытки;

		ТекущийПринтер = "";
		Если СразуНаПринтер=1 Тогда
			Принтер.ПолучитьЗначение(Принтер.ТекущаяСтрока(),ТекущийПринтер);
		КонецЕсли;

		Параметры = СоздатьОбъект("СписокЗначений");
		//Параметры.ДобавитьЗначение(глВзятьКонтекст(Контекст), "Контекст");
		Параметры.ДобавитьЗначение(Док.ТекущийДокумент(), "Контекст");
		//Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");

		Параметры.ДобавитьЗначение(ТекущийПринтер,	"Устройство");
		Параметры.ДобавитьЗначение(1,				"КоличествоКопий");
		Параметры.ДобавитьЗначение(СразуНаПринтер,	"СразуПечать");
		Параметры.ДобавитьЗначение(1,				"НеПоказыватьДиалог");
		Если СразуНаПринтер=1 Тогда
			Параметры.ДобавитьЗначение("ПечатьНаПринтер",	"Команда");
		Иначе
			Параметры.ДобавитьЗначение("ПечатьНаЭкран",	"Команда");
		КонецЕсли;

		//НомерТекущейФормы = ПечФорма.ТекущаяСтрока();
		//Если НомерТекущейФормы = 1  Тогда
		//	//глПечатьДокумента(Док.ТекущийДокумент(), Параметры); //?(СразуНаПринтер=0,"ПечатьНаЭкран","ПечатьНаПринтер")
		//	ОткрытьФорму(Док.ТекущийДокумент(),Параметры,1);
		//Иначе
			Сообщить("Печать "+Док);
			ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+"ПКО.ert");
		//КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
Процедура Реестр()
	Таб = СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Шапка");

	Док = СоздатьОбъект("Документ.ПКО");
	Док.ВыбратьДокументы(НачДата,КонДата);

	Ном=0;
	Пока Док.ПолучитьДокумент()=1 Цикл
		Если Док.ПометкаУдаления()=1 Тогда Продолжить;  КонецЕсли;

		Попытка
			Если Выбклиент.Выбран()=1 Тогда
				Если НЕ (Док.Контрагент = Выбклиент) Тогда Продолжить; КонецЕсли;
			КонецЕсли;
		Исключение КонецПопытки;
		Попытка
			Если ВыбАвтор.Выбран()=1 Тогда
				Если НЕ (Док.Автор = ВыбАвтор) Тогда Продолжить; КонецЕсли;
			КонецЕсли;
		Исключение КонецПопытки;

		Ном=Ном+1;
		Сумма="";
		Попытка
			Сумма = Док.СуммаВзаиморасчетов;
		Исключение
			Попытка
				Сумма = Док.ДокОснование.СуммаВзаиморасчетов;
			Исключение
			КонецПопытки
		КонецПопытки;
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;

	Таб.ТолькоПросмотр(1);
	Таб.Показать("Реестр");
КонецПроцедуры

//}

//******************************************************************************

НомерПорта = 6;
НачДата = ТекущаяДата();
КонДата = ТекущаяДата();
ВыбАвтор = глПользователь;
ВыбКасса = глПользователь.ОсновнаяКасса;

тзДокументы = СоздатьОбъект("ТаблицаЗначений");
тзДокументы.НоваяКолонка();
тзДокументы.НоваяКолонка("Пометка",,,,,5,);
тзДокументы.НоваяКолонка("Док",,,,,30,);
тзДокументы.НоваяКолонка("Контрагент",,,,,20,);
//тзДокументы.НоваяКолонка("Грузополучатель",,,,,20,);
тзДокументы.НоваяКолонка("Сумма","Число",10,3,,10,);
тзДокументы.НоваяКолонка("НомерЧека",,,,,10,);
//тзДокументы.НоваяКолонка("Склад",,,,,10,);


Сервис = СоздатьОбъект("Сервис");
Сервис.ВключитьРаскраскуТаблиц();
Сервис.ИспользоватьПланРаскраски(0);

Атол10 = СоздатьОбъект("Атол10");